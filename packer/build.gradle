apply plugin: 'com.android.application'
apply plugin: "com.github.lanchon.dexpatcher.base"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.pangxiezhou.packjar"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        warning 'InvalidPackage'
    }
}

dexpatcherConfig {
    dir = rootProject.file('dexpatcher-gradle-tools')
}

def projectTargetLib = hasProperty('targetLib')?getProperty('targetLib'):'libB'

dependencies {
    compile project(":$projectTargetLib")
}

task unzipAPK(type: Copy, dependsOn: build){
    from(zipTree(new File(buildDir, 'outputs/apk/packer-debug.apk'))) {
        include 'classes.dex'
    }

    into file('unzip_dex')
}

task packJar(type: lanchon.dexpatcher.gradle.tasks.Dex2jarTask, dependsOn: unzipAPK) {
    dexFiles = file('unzip_dex/classes.dex')
    outputFile = file("release_jar/${projectTargetLib}.jar")

    doLast {
        delete file('unzip_dex')
    }

    doFirst {
        delete file('release_jar')
    }
}
